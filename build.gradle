plugins {
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "5.2.0" apply false
    id "org.embulk.embulk-plugins" version "0.4.2"
}

repositories {
    mavenCentral()
    jcenter()
}
configurations {
    provided
}

group = "io.trocco"
version = "0.0.11"
description = "Pardot input plugin for Embulk"
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"
    compile project(path: ":shadow-pardot-helper", configuration: "shadow")
    testCompile "junit:junit:4.13"
    testCompile 'org.embulk:embulk-standards:0.9.23'
    testCompile 'org.embulk:embulk-test:0.9.23'
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "org.embulk:embulk-core:0.9.23:tests"
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
}

embulkPlugin {
    mainClass = "org.embulk.input.pardot.PardotInputPlugin"
    category = "input"
    type = "pardot"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {
            // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gem {
    from("LICENSE")  // Optional -- if you need other files in the gem.
    authors = ["u110"]
    email = ["web.u.project@gmail.com"]
    summary = "${description}"
    homepage = "https://github.com/u110/embulk-input-pardot"
    licenses = ["Apache-2.0"]
}

gemPush {
    host = "https://rubygems.pkg.github.com/u110"
}
